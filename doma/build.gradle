apply plugin: 'java'
apply plugin: 'idea'

processResources.destinationDir = compileJava.destinationDir
compileJava.dependsOn processResources

compileJava {
  options.encoding = 'utf-8'
}

sourceCompatibility = targetCompatibility = 1.8

repositories {
  jcenter()
}

dependencies {
  compile 'org.seasar.doma:doma:2.4.1'
  compile 'ch.qos.logback:logback-classic:1.1.3'
  compile 'javax.inject:javax.inject:1'
  compile 'com.google.inject:guice:4.0'
  runtime 'com.h2database:h2:1.3.175'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-library:1.3'
  testCompile 'org.flywaydb:flyway-core:3.2.1'
}

compileJava.options.compilerArgs = ['-Adoma.domain.converters=trial.Converters']

idea {
  module {
    sourceDirs += file 'generated'
    outputDir = compileJava.destinationDir
  }
  project.ipr.withXml { provider ->
    def compilerConfig = provider.node.find {
      it.'@name' == 'CompilerConfiguration'
    }
    def annotationProcessing = compilerConfig.annotationProcessing[0]
    annotationProcessing.attributes().clear()
    def profile = annotationProcessing.appendNode 'profile', [default:true, name:'Default', enabled:true]
    profile.appendNode 'outputRelativeToContentRoot', [value:true]
    profile.appendNode 'processorPath', [useClasspath:true]
  }
}
